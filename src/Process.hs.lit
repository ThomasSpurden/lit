# [/ ](root.html)Process.hs - links `lit`'s command line interface and file generation

Process unifies the different ways that `lit` can produce files. Here are the goals:

1. Parse a file into a data structure
2. Pass the data structure through pipelines (Html, Code, Markdown)
3. Write the output to files

An overview of the file:
    << * >>=
    << define Process module >>
    << import modules >>
    << process a single file >>
    << helper functions >>

Process exports the pipelines so they can be *preconfigured*. In `lit.hs`, the pipelines are curried with the output directory corresponding to their type, and with the optional css path. The `process` function depends on being called with preconfigured pipes.     
    << define Process module >>=
    {-# LANGUAGE OverloadedStrings #-}
    module Process
    ( process
    , htmlPipeline
    , mdPipeline
    , codePipeline ) where

Besides the reading and writing file utilities, each output format is contained in a module which specifies a `generate` function. Ex. `Html.generate` renders the parsed `.lit` file as `Text`.
    << import modules >>=
    import Prelude hiding (readFile, writeFile)
    import Data.Text.IO (writeFile, readFile)
    import System.FilePath.Posix (takeFileName, dropExtension)
    import qualified Data.Text as T

    import Parse (encode)
    import Code
    import Html
    import Markdown
    import Types

Process as a library accompolishes the primary goals through the `process` function. After reading the file, `encode` parses the file into `[Chunks]` (see [Types.hs](Types.hs.html) for more about the data structure). Lastly, each pipeline function in the list of `pipes` is applied to the data structure. Each pipeline takes a `[Chunks]` and writes a file. 
    << process a single file >>=
    process pipes file = do 
    stream <- readFile file
    encoded <- return $ encode stream 
    mapM_ (\f -> f fileName encoded) pipes >> return ()
    where
        fileName = dropExtension $ takeFileName file

Beside the main function `process`. Process as a module defines several useful pipelines (function transforms chained together). Each pipeline can be generalized in the following way.

1. Append the correct file extention to the filename 
2. Transform `[Chunks] -> Text`, through a `generate` function 
3. Write to the path with the transformed text

    << helper functions >>=
    << html pipeline >>
    << markdown pipeline >>
    << code pipeline >>
    << check that directories end with '/' >>

    << html pipeline >>=
    htmlPipeline dir css name enc = writeFile path output
        where 
            path = (ensureTrailingSlash dir) ++ name ++ ".html"
            output = Html.generate css name enc

    << markdown pipeline >>=
    mdPipeline dir css name enc = writeFile path output
        where
            path = (ensureTrailingSlash dir) ++ name ++ ".md"
            output = Markdown.generate name enc

    << code pipeline >>=
    codePipeline dir css name enc = writeFile path output
        where
            path = (ensureTrailingSlash dir) ++ name
            output = Code.generate enc

This last utility function solves the ambiguity in specifying a directory with a command line tool. Ex. `lit -c --code-dir=output ..` behaves the same as `lit -c --code-dir=output/ ..`
    << check that directories end with '/' >>=
    ensureTrailingSlash dir = 
        if last dir == '/'
        then dir
        else dir ++ "/"
